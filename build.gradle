apply plugin: 'groovy'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'io.github.plotnik.bookindex'
}

repositories {
    mavenCentral()
}

jar {
    baseName = 'bookindex'

    manifest {
        attributes "Main-Class": "io.github.plotnik.bookindex"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.14'
    compile 'org.apache.pdfbox:pdfbox:2.0.9'
    compile 'org.apache.httpcomponents:httpclient:4.5.5'
    compile 'org.apache.httpcomponents:httpmime:4.5.5'
}

/* Asciidoclet links:
 *   http://mrhaki.blogspot.com/2015/10/awesome-asciidoctor-using-asciidoctor.html
 *   https://github.com/asciidoctor/asciidoclet
 *   http://docs.oracle.com/javase/7/docs/technotes/tools/windows/javadoc.html
 *   https://docs.gradle.org/current/javadoc/org/gradle/external/javadoc/StandardJavadocDocletOptions.html
 */

configurations {
    asciidoclet
}

dependencies {
    asciidoclet 'org.asciidoctor:asciidoclet:1.+'
}

javadoc {
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.overview = "src/main/groovy/overview.adoc"
    options.links = [
        'http://docs.oracle.com/javase/8/docs/api/',
        'https://hc.apache.org/httpcomponents-client-ga/httpclient/apidocs/'
    ]
    options.noQualifiers = ['java.lang', 'java.util']
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.addStringOption "-base-dir", "${projectDir}"
}
